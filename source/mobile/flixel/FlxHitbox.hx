﻿package mobile.flixel;

import openfl.Lib;
import openfl.display.Shape;
import openfl.display.BitmapData;
import flixel.input.keyboard.FlxKey;

import mobile.flixel.input.FlxMobileInputManager;
import mobile.flixel.FlxButton;

/**
 * A zone with dynamic hint's based on mania.
 * 
 * @author: Mihai Alexandru
 * @modification's author: Karim Akra & Lily (mcagabe19)
 */
class FlxHitbox extends FlxMobileInputManager
{
	public var buttonNotes:Array<FlxButton> = [];
	public var buttonExtra:Array<FlxButton> = [];

	var storedButtonsIDs:Map<String, Array<FlxKey>> = new Map<String, Array<FlxKey>>();

	/**
	 * Create the zone.
	 */
	public function new()
	{
		super();

		// Get mania value (default to 3 for 4K)
		var mania:Int = 3;
		if (PlayState.SONG != null && PlayState.SONG.mania != null)
		{
			mania = PlayState.SONG.mania;
		}
		var keys:Int = mania + 1; // Number of keys

		var stage = Lib.current.stage;

		var scale:Float = Math.min(stage.stageWidth / 1280, stage.stageHeight / 720);
		var newWidth:Int = Std.int(stage.stageWidth / scale);
		var newHeight:Int = Std.int(stage.stageHeight / scale);

		for (button in Reflect.fields(this))
		{
			if (Std.isOfType(Reflect.field(this, button), FlxButton))
			{
				storedButtonsIDs.set(button, Reflect.getProperty(Reflect.field(this, button), 'IDs'));
			}
		}

		if (ClientPrefs.data.extraKey == 0)
		{
			for (i in 0...keys)
			{
				var button = createHint(newWidth * i / keys, 0, Std.int(newWidth / keys), Std.int(newHeight), getColor(i, mania));
				buttonNotes.push(button);
				add(button);
			}
		}
		else
		{
			if (ClientPrefs.data.hitboxLocation == 'Bottom')
			{

				for (i in 0...keys)
				{
					var button = createHint(newWidth * i / keys, 0, Std.int(newWidth / keys), Std.int(newHeight * 0.8), getColor(i, mania));
					buttonNotes.push(button);
					add(button);
				}

				for (i in 0...ClientPrefs.data.extraKey)
				{
					var button = createHint(i * Std.int(newWidth / ClientPrefs.data.extraKey), (newHeight / 5) * 4, Std.int(newWidth / ClientPrefs.data.extraKey), Std.int(newHeight / 5), 0xFFFF00);
					buttonExtra.push(button);
					add(button);
				}
			}
			else
			{
				for (i in 0...keys)
				{
					var button = createHint(newWidth * i / keys, newHeight * 0.2, Std.int(newWidth / keys), Std.int(newHeight * 0.8),
						getColor(i, mania));
					buttonNotes.push(button);
					add(button);
				}

				for (i in 0...ClientPrefs.data.extraKey)
				{
					var button = createHint(i * Std.int(newWidth / ClientPrefs.data.extraKey), 0, Std.int(newWidth / ClientPrefs.data.extraKey), Std.int(newHeight / 5), 0xFFFF00);
					buttonExtra.push(button);
					add(button);
				}
			}
		}

		// Assign input IDs to main keys
		for (i in 0...buttonNotes.length)
		{
			buttonNotes[i].IDs = getInputID(mania, i);
		}

		// Assign input IDs to extra buttons
		for (button in Reflect.fields(this))
		{
			if (Std.isOfType(Reflect.field(this, button), FlxButton))
			{
				Reflect.setProperty(Reflect.getProperty(this, button), 'IDs', storedButtonsIDs.get(button));
			}
		}

		scrollFactor.set();
		updateTrackedButtons();
	}

	/**
	 * Get input ID based on mania and index
	 */
	private function getInputID(mania:Int, index:Int):Array<FlxKey>
	{
		var key:String = '';
		if (mania == 3) {
			switch (index) {
				case 0: key = 'note_left';
				case 1: key = 'note_down';
				case 2: key = 'note_up';
				case 3: key = 'note_right';
			}
		} else {
			key = '${mania}_key_${index}';
		}

		if (ClientPrefs.keyBinds.exists(key))
			return ClientPrefs.keyBinds.get(key);

		return [];
	}

	/**
	 * Get color for key based on index and mania
	 */
	private function getColor(index:Int, mania:Int):Int
	{
		// 动态颜色设置优先
		if (ClientPrefs.data.dynamicColors)
		{
			var keyMode = ExtraKeysHandler.instance.data.keys[mania];
			if (keyMode != null)
			{
				var noteIndex = keyMode.notes[index];
				if (ClientPrefs.data.arrowRGB != null && noteIndex < ClientPrefs.data.arrowRGB.length)
				{
					return ClientPrefs.data.arrowRGB[noteIndex][0];
				}
			}
		}
		
		// 使用extrakeys.json中的默认颜色
		var keyMode = ExtraKeysHandler.instance.data.keys[mania];
		if (keyMode != null)
		{
			var noteIndex = keyMode.notes[index];
			if (noteIndex < ExtraKeysHandler.instance.data.colors.length)
			{
				var colorObj = ExtraKeysHandler.instance.data.colors[noteIndex];
				return FlxColor.fromString('#' + colorObj.inner);
			}
		}

		return switch (mania)
		{
			case 0: 0xFFC24B99; // 1K - Purple
			case 1: index == 0 ? 0xFFC24B99 : 0xFFF9393F; // 2K - Purple/Red
			case 2: [0xFFC24B99, 0xFF12FA05, 0xFFF9393F][index]; // 3K - Purple/Green/Red
			default: [0xFFC24B99, 0xFF00FFFF, 0xFF12FA05, 0xFFF9393F][index % 4]; // 4K+ colors
		}
	}

	/**
	 * Clean up memory.
	 */
	override function destroy()
	{
		super.destroy();

		for (button in buttonNotes)
		{
			button = FlxDestroyUtil.destroy(button);
		}
		for (button in buttonExtra)
		{
			button = FlxDestroyUtil.destroy(button);
		}
	}

	private function createHint(X:Float, Y:Float, Width:Int, Height:Int, Color:Int = 0xFFFFFF):FlxButton
	{
		var hint = new FlxButton(X, Y);
		hint.loadGraphic(createHintGraphic(Width, Height));
		hint.color = Color;
		hint.solid = false;
		hint.immovable = true;
		hint.multiTouch = true;
		hint.moves = false;
		hint.scrollFactor.set();
		hint.alpha = 0.5;
		hint.antialiasing = ClientPrefs.data.antialiasing;
		
		if (ClientPrefs.data.playControlsAlpha >= 0)
		{
			hint.onDown.callback = function()
			{
				hint.alpha = ClientPrefs.data.playControlsAlpha;
			}
			hint.onUp.callback = function()
			{
				hint.alpha = 0.00001;
			}
			hint.onOut.callback = function()
			{
				hint.alpha = 0.00001;
			}
		}
		#if FLX_DEBUG
		hint.ignoreDrawDebug = true;
		#end
		return hint;
	}

	function createHintGraphic(Width:Int, Height:Int):BitmapData
	{
		var shape:Shape = new Shape();

		var guh = ClientPrefs.data.playControlsAlpha;
		if (guh >= 0.9)
			guh = ClientPrefs.data.playControlsAlpha - 0.07;

		shape.graphics.beginFill(0xFFFFFF);
		shape.graphics.lineStyle(3, 0xFFFFFF, 1);
		shape.graphics.drawRect(0, 0, Width, Height);
		shape.graphics.lineStyle(0, 0, 0);
		shape.graphics.drawRect(3, 3, Width - 6, Height - 6);
		shape.graphics.endFill();
		shape.graphics.drawRect(3, 3, Width - 6, Height - 6);
		shape.graphics.endFill();

		var bitmap:BitmapData = new BitmapData(Width, Height, true, 0);
		bitmap.draw(shape);
		return bitmap;
	}
}

